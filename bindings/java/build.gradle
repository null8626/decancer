plugins {
  id 'java'
  id 'maven-publish'
}

group 'io.github.null8626'
version '3.0.0'
description 'A tiny package that removes common unicode confusables/homoglyphs from strings.'

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

jar {
  archiveBaseName = 'decancer'
  archiveVersion = '3.0.0'

  from('./bin') {
    include '*.dll'
    include '*.dylib'
    include '*.so'
  }
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'io.github.null8626'
      artifactId 'decancer'
      version '3.0.0'
      
      from components.java
      
      artifact sourcesJar
      artifact javadocJar
      
      pom {
        name = 'decancer'
        description 'A tiny package that removes common unicode confusables/homoglyphs from strings.'
        url = 'https://github.com/null8626/decancer'
        inceptionYear = '2021'
        
        licenses {
          license {
            name = 'MIT License'
            distribution = 'repo'
            url = 'https://github.com/null8626/decancer/blob/v3.0.0/LICENSE'
          }
        }
        
        developers {
          developer {
            id = 'null8626'
            name = 'null8626'
            url = 'https://github.com/null8626'
            timezone = 'Asia/Jakarta'
          }
        }
        
        scm {
          url = 'https://github.com/null8626/decancer'
          connection = 'scm:git:git://github.com/null8626/decancer'
          developerConnection = 'scm:git:git://github.com/null8626/decancer'
        }
        
        issueManagement {
          system = 'GitHub'
          url = 'https://github.com/null8626/decancer/issues'
        }
        
        ciManagement {
          system = 'Github Actions'
          url = 'https://github.com/null8626/decancer/actions'
        }
      }
    }
  }
}

javadoc {
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

test {
  useJUnitPlatform()
  
  systemProperty 'DECANCER_INSIDE_JAR', System.getProperty('DECANCER_INSIDE_JAR')
  systemProperty 'java.library.path', "${System.getProperty('java.library.path')}${File.pathSeparatorChar}$rootDir/bin"
}

dependencies {
  implementation 'com.github.adamheinrich:native-utils:e6a3948966'
  implementation 'com.fizzed:jne:4.1.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}