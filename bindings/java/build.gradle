plugins {
  id 'java'
  id 'maven-publish'
}

group 'com.github.null8626'
version '3.2.0'
description 'A tiny package that removes common unicode confusables/homoglyphs from strings.'

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

jar {
  archiveBaseName = 'decancer'
  archiveVersion = '3.2.0'

  from('./bin') {
    include '*.dll'
    include '*.dylib'
    include '*.so'
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'com.github.null8626'
      artifactId 'decancer'
      version '3.2.0'
      
      from components.java
      
      pom {
        name = 'decancer'
        description 'A tiny package that removes common unicode confusables/homoglyphs from strings.'
        url = 'https://github.com/null8626/decancer'
        
        licenses {
          license {
            name = 'MIT License'
            url = 'https://github.com/null8626/decancer/blob/v3.2.0/LICENSE'
          }
        }
        
        developers {
          developer {
            id = 'null8626'
            name = 'null8626'
          }
        }
      }
    }
  }
}

javadoc {
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

test {
  useJUnitPlatform()
  
  systemProperty 'DECANCER_INSIDE_JAR', '0'
  systemProperty 'java.library.path', "${System.getProperty('java.library.path')}${File.pathSeparatorChar}$rootDir/bin"
}

dependencies {
  implementation 'com.github.adamheinrich:native-utils:e6a3948966'
  implementation 'com.fizzed:jne:4.1.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}