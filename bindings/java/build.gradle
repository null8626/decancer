plugins {
  id 'java-library'
  id 'maven-publish'
  id 'org.jreleaser' version '1.20.0'
}

group = 'io.github.null8626'
version = '3.3.3'
description = 'A library that removes common unicode confusables/homoglyphs from strings.'

repositories {
  mavenCentral()
  maven { url = 'https://jitpack.io' }
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'decancer'
      groupId = 'io.github.null8626'
      version = '3.3.3'
    
      from components.java
      
      pom {
        name = 'decancer'
        description = 'A library that removes common unicode confusables/homoglyphs from strings.'
        url = 'https://github.com/null8626/decancer'
        inceptionYear = '2021'
        
        licenses {
          license {
            name = 'MIT License'
            distribution = 'repo'
            url = 'https://github.com/null8626/decancer/blob/v3.3.3/LICENSE'
          }
        }
        
        developers {
          developer {
            id = 'null8626'
            name = 'null8626'
            url = 'https://github.com/null8626'
            timezone = 'Asia/Jakarta'
          }
        }
        
        scm {
          url = 'https://github.com/null8626/decancer'
          connection = 'scm:git:git://github.com/null8626/decancer'
          developerConnection = 'scm:git:git://github.com/null8626/decancer'
        }
        
        issueManagement {
          system = 'GitHub'
          url = 'https://github.com/null8626/decancer/issues'
        }
        
        ciManagement {
          system = 'Github Actions'
          url = 'https://github.com/null8626/decancer/actions'
        }
      }
    }
  }
  
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

jreleaser {
  project {
    name = 'decancer'
    version = '3.3.3'
    description = 'A library that removes common unicode confusables/homoglyphs from strings.'
    longDescription = 'A library that removes common unicode confusables/homoglyphs from strings. By default, the library supports filtering over 220,000 unicode codepoints and reordering right-to-left text as it were to be rendered graphically.'
    license = 'MIT'
    inceptionYear = '2021'
    copyright = 'Copyright (c) 2021-2026 null8626'
  }

  signing {
    active = 'ALWAYS'
    armored = true
  }
  
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          gitRootSearch = true
          maxRetries = 250
          stagingRepository(layout.buildDirectory.dir('staging-deploy').get().toString())
        }
      }
    }
  }
  
  release {
    github {
      skipTag = true
      skipRelease = true
    }
  }
}

java {
  withJavadocJar()
  withSourcesJar()

  jar {
    archiveBaseName = 'decancer'
    archiveVersion = '3.3.3'
  
    from('./bin') {
      include '*.dll'
      include '*.dylib'
      include '*.so'
    }
  }

  javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }
}

test {
  useJUnitPlatform()
  
  systemProperty 'DECANCER_TESTING', System.getProperty('DECANCER_TESTING')
  systemProperty 'java.library.path', "${System.getProperty('java.library.path')}${File.pathSeparatorChar}$rootDir/bin"
}

dependencies {
  implementation 'com.github.adamheinrich:native-utils:e6a3948'
  implementation 'com.fizzed:jne:4.5.3'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.0'
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:6.0.0"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:6.0.0"
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:deprecation"
}