plugins {
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id 'org.jreleaser' version '1.12.0'
}

group 'io.github.null8626'
version '3.0.0'
description 'A tiny package that removes common unicode confusables/homoglyphs from strings.'

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

jar {
  archiveBaseName = 'decancer'
  archiveVersion = '3.0.0'

  from('./bin') {
    include '*.dll'
    include '*.dylib'
    include '*.so'
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'decancer'
      groupId = 'io.github.null8626'
      version = '3.0.0'
    
      from components.java
      
      pom {
        name = 'decancer'
        description = 'A tiny package that removes common unicode confusables/homoglyphs from strings.'
        url = 'https://github.com/null8626/decancer'
        inceptionYear = '2021'
        
        licenses {
          license {
            name = 'MIT License'
            distribution = 'repo'
            url = 'https://github.com/null8626/decancer/blob/v3.0.0/LICENSE'
          }
        }
        
        developers {
          developer {
            id = 'null8626'
            name = 'null8626'
            url = 'https://github.com/null8626'
            timezone = 'Asia/Jakarta'
          }
        }
        
        scm {
          url = 'https://github.com/null8626/decancer'
          connection = 'scm:git:git://github.com/null8626/decancer'
          developerConnection = 'scm:git:git://github.com/null8626/decancer'
        }
        
        issueManagement {
          system = 'GitHub'
          url = 'https://github.com/null8626/decancer/issues'
        }
        
        ciManagement {
          system = 'Github Actions'
          url = 'https://github.com/null8626/decancer/actions'
        }
      }
    }
  }
  
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
  }
  
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('target/staging-deploy')
        }
      }
    }
  }
}

javadoc {
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform()
  
  dependsOn cleanTest
  testLogging.showStandardStreams = true
  
  systemProperty 'DECANCER_TESTING', System.getProperty('DECANCER_TESTING')
  systemProperty 'java.library.path', "${System.getProperty('java.library.path')}${File.pathSeparatorChar}$rootDir/bin"
}

dependencies {
  implementation 'com.github.adamheinrich:native-utils:e6a3948966'
  implementation 'com.fizzed:jne:4.1.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}